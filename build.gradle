plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'maven-publish'
}

group = 'com.realmmc.controller'
version = '1.0'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    implementation("com.velocitypowered:velocity-api:3.4.0-SNAPSHOT")
    annotationProcessor("com.velocitypowered:velocity-api:3.4.0-SNAPSHOT")
    implementation("com.fasterxml.jackson.core:jackson-databind:2.20.0")
    compileOnly("org.projectlombok:lombok:1.18.40")
    annotationProcessor("org.projectlombok:lombok:1.18.40")
    implementation("redis.clients:jedis:6.1.0")
    implementation("net.kyori:adventure-text-minimessage:4.24.0")
    implementation("net.kyori:adventure-text-serializer-legacy:4.14.0")
    implementation("org.mongodb:mongodb-driver-sync:4.11.1")
    compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT'
    implementation("com.github.retrooper.packetevents:spigot:2.3.0")
    implementation("com.github.retrooper.packetevents:api:2.3.0")
}
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
    sourceCompatibility = JavaVersion.VERSION_24
    targetCompatibility = JavaVersion.VERSION_24
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.shadowJar {
    archiveBaseName.set('controller')
    archiveClassifier.set('')
    archiveVersion.set(project.version.toString())
    mergeServiceFiles()
    exclude('META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA')
}

tasks.build {
    dependsOn tasks.shadowJar
}

tasks.withType(PublishToMavenRepository).configureEach {
    dependsOn tasks.shadowJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group.toString()
            artifactId = 'controller'
            version = project.version.toString()
            artifact(tasks.shadowJar)
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            def repo = System.getenv("GITHUB_REPOSITORY") ?: "Realm-MC/controller"
            url = uri("https://maven.pkg.github.com/${repo}")

            credentials {
                username = findProperty("gpr.user")?.toString() ?: System.getenv("GITHUB_ACTOR")
                password = findProperty("gpr.token")?.toString() ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
